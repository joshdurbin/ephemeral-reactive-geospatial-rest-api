buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:1.5.1'
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    classpath 'com.bmuschko:gradle-docker-plugin:3.0.11'
  }
}

plugins {
  id 'com.palantir.git-version' version '0.10.0'
}

apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'com.palantir.git-version'

repositories {
  jcenter()
}

dependencies {

  // non-blocking sl4fj impl
  runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.10.0'
  runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
  runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
  runtime group: 'com.lmax', name: 'disruptor', version: '3.3.7'

  // required by hibernate validator
  runtime group: 'org.glassfish', name: 'javax.el', version: '3.0.1-b09'

  compile group: 'org.hibernate', name: 'hibernate-validator', version: '6.0.7.Final'
  compile group: 'com.github.davidmoten', name: 'grumpy-core', version: '0.2.3'
  compile group: 'com.github.davidmoten', name: 'rtree', version: '0.8.0.4'
  compile group: 'com.github.gregwhitaker', name: 'ratpack-error', version: '1.5.0'

  compile ratpack.dependency('rx')

  testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
}

docker {

  if (System.getenv('CI')) {
    url = System.getenv('DOCKER_HOST')
    certPath = new File(System.getenv('DOCKER_CERT_PATH'))

    registryCredentials {
      username = System.getenv('DOCKER_USERNAME')
      password = System.getenv('DOCKER_PASSWORD')
      email = System.getenv('DOCKER_EMAIL')
    }
  }

  javaApplication {
    maintainer = 'Josh Durbin "durbinjo593@gmail.com"'
    port = 5050
    baseImage = 'anapsix/alpine-java:8_server-jre'
    tag = 'joshdurbin/ephemeral-reactive-geospatial-rest-api:latest'
  }
}